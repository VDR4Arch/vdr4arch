# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=oscam
pkgver=11390
_gitrev=22c9f6ef27609e8fae7db99fda43d2b82a263ed3
pkgrel=1
pkgdesc="Open Source Conditional Access Module software"
url="http://www.streamboard.tv/oscam"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL3')
depends=('libusb' 'openssl' 'pcsclite')
makedepends=('git')
optdepends=('ccid: PC/SC reader generic driver')
install='oscam.install'
source=("git+http://www.oscam.cc/git/oscam-mirror#commit=$_gitrev"
        "http://www.streamboard.tv/oscam/raw-attachment/ticket/4590/reader-cryptoworks.patch"
        'oscam.service'
        'oscam.sysuser')
# "${pkgname}-${pkgver}.zip::http://www.streamboard.tv/oscam/changeset/${pkgver}/trunk?old_path=%2F&old=${pkgver}&format=zip"
# noextract=("${pkgname}-${pkgver}.zip")
md5sums=('SKIP'
         'cb51eea8f5da9f6bfe5a0a3709831aaa'
         '596b902e3f4a66d39e7f993437feec74'
         'be0d9d7a5fdd8cf4918c4ea91cebd989')

pkgver() {
  cd "$srcdir/$pkgname-mirror"
  git log -1 | grep git-svn-id | cut -d'@' -f2 | cut -d' ' -f1
}

prepare() {
#   cat "${pkgname}-${pkgver}.zip" | bsdtar -xf -

#   cd "$srcdir/trunk"
#   chmod +x config.sh webif/pages_mkdep
  cd "$srcdir/$pkgname-mirror"
  patch -p1 -i "$srcdir/reader-cryptoworks.patch"
}

build() {
  cd "$srcdir/$pkgname-mirror"
#   cd "$srcdir/trunk"

  make CONF_DIR=/var/lib/oscam \
       USE_SSL=1 \
       USE_LIBUSB=1 \
       USE_PCSC=1 \
       OSCAM_BIN=oscam \
       LIST_SMARGO_BIN=list_smargo \
       SVN_REV=$pkgver
}

package() {
  cd "$srcdir/$pkgname-mirror"
#   cd "$srcdir/trunk"

  #Install binaries
  install -Dm755 oscam "$pkgdir/usr/bin/oscam"
  install -Dm755 list_smargo "$pkgdir/usr/bin/list_smargo"

  #Install man-pages
  mkdir -p $pkgdir/usr/share/man/man1/
  mkdir -p $pkgdir/usr/share/man/man5/
  install -Dm644 Distribution/doc/man/*.1 "$pkgdir/usr/share/man/man1"
  install -Dm644 Distribution/doc/man/*.5 "$pkgdir/usr/share/man/man5"

  #Install service file
  install -Dm644 ${srcdir}/oscam.service "$pkgdir/usr/lib/systemd/system/oscam.service"

  #Install sysuser config
  install -Dm644 ${srcdir}/oscam.sysuser "$pkgdir/usr/lib/sysusers.d/oscam.conf"
}
