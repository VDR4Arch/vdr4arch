# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=oscam
pkgver=11769
_gitver=a1cd3e668f20e917e56e9e3aff5d3d994fd7d8ab
pkgrel=1
pkgdesc="Open Source Conditional Access Module software"
url='https://wiki.streamboard.tv/wiki/OSCam'
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL-3.0-or-later')
depends=('libusb' 'openssl' 'pcsclite')
makedepends=('git')
optdepends=('ccid: PC/SC reader generic driver')
install='oscam.install'
source=("git+https://git.streamboard.tv/common/oscam.git#commit=${_gitver}"
        'oscam.service'
        'oscam.sysuser')
sha256sums=('cccf3024f5a085cba3ba4953501a20947faad532b54c92b9694aa5a66871a953'
            'ad08ee6fc95909b8b9f10c804a8158901383548c27720cf863a868114f45887f'
            '3adbbfa4f388c6510b365719da251360dee9cc2885c5f82e06d56c246fcd26b0')

pkgver() {
  cd "$srcdir/$pkgname"
  ./config.sh --oscam-revision
}

build() {
  cd "$srcdir/$pkgname"

  ./config.sh --enable \
              WITH_SSL \
              IPV6SUPPORT \
              CARDREADER_SMARGO

  make CONF_DIR=/var/lib/oscam \
       USE_SSL=1 \
       USE_LIBUSB=1 \
       USE_PCSC=1 \
       OSCAM_BIN=oscam \
       LIST_SMARGO_BIN=list_smargo
}

package() {
  cd "$srcdir/$pkgname"

  #Install binaries
  install -Dm755 oscam "$pkgdir/usr/bin/oscam"
  install -Dm755 list_smargo "$pkgdir/usr/bin/list_smargo"

  #Install man-pages
  mkdir -p $pkgdir/usr/share/man/man1/
  mkdir -p $pkgdir/usr/share/man/man5/
  install -Dm644 Distribution/doc/man/*.1 "$pkgdir/usr/share/man/man1"
  install -Dm644 Distribution/doc/man/*.5 "$pkgdir/usr/share/man/man5"

  #Install service file
  install -Dm644 ${srcdir}/oscam.service "$pkgdir/usr/lib/systemd/system/oscam.service"

  #Install sysuser config
  install -Dm644 ${srcdir}/oscam.sysuser "$pkgdir/usr/lib/sysusers.d/oscam.conf"
}
