# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=oscam
pkgver=11532
pkgrel=2
pkgdesc="Open Source Conditional Access Module software"
url='https://www.streamboard.tv/oscam'
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL3')
depends=('libusb' 'openssl-1.1' 'pcsclite')
makedepends=('subversion')
optdepends=('ccid: PC/SC reader generic driver')
install='oscam.install'
source=("oscam::svn+https://svn.streamboard.tv/oscam/trunk#revision=${pkgver}"
        'oscam.service'
        'oscam.sysuser')
md5sums=('SKIP'
         '596b902e3f4a66d39e7f993437feec74'
         'be0d9d7a5fdd8cf4918c4ea91cebd989')

build() {
  cd "$srcdir/$pkgname"

  ./config.sh --enable \
              WITH_SSL \
              IPV6SUPPORT \
              CARDREADER_SMARGO

  make CONF_DIR=/var/lib/oscam \
       USE_SSL=1 \
       SSL_CFLAGS='-DWITH_SSL=1 -I/usr/include/openssl-1.1' \
       SSL_LIB='-lssl -L/usr/lib/openssl-1.1' \
       USE_LIBUSB=1 \
       USE_PCSC=1 \
       OSCAM_BIN=oscam \
       LIST_SMARGO_BIN=list_smargo \
       SVN_REV=$pkgver
}

package() {
  cd "$srcdir/$pkgname"

  #Install binaries
  install -Dm755 oscam "$pkgdir/usr/bin/oscam"
  install -Dm755 list_smargo "$pkgdir/usr/bin/list_smargo"

  #Install man-pages
  mkdir -p $pkgdir/usr/share/man/man1/
  mkdir -p $pkgdir/usr/share/man/man5/
  install -Dm644 Distribution/doc/man/*.1 "$pkgdir/usr/share/man/man1"
  install -Dm644 Distribution/doc/man/*.5 "$pkgdir/usr/share/man/man5"

  #Install service file
  install -Dm644 ${srcdir}/oscam.service "$pkgdir/usr/lib/systemd/system/oscam.service"

  #Install sysuser config
  install -Dm644 ${srcdir}/oscam.sysuser "$pkgdir/usr/lib/sysusers.d/oscam.conf"
}
