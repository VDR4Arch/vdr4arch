# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinflatplus
pkgver=0.2.0
_vdrapi=2.0.6
pkgrel=2
pkgdesc="Simple and slim skin for VDR"
url="http://projects.vdr-developer.org/projects/plg-skinflatplus"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://projects.vdr-developer.org/attachments/download/1653/$_plugname-$pkgver.tar.gz")
md5sums=('d8b2b57a7d1bedb5b05de0543282ae07')

prepare() {
  cd "$srcdir/$_plugname-$pkgver"
  sed -i 's/Magick++/GraphicsMagick++/g' Makefile
}

build() {
  cd "$srcdir/$_plugname-$pkgver"
  make
}

package() {
  cd "$srcdir/$_plugname-$pkgver"

  make DESTDIR="$pkgdir" install
}
