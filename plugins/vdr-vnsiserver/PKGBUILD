# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-vnsiserver
pkgver=12.0_Frodo
_gitver=96774c4f775b156a46fb58151379dece3e773c96
_vdrapi=2.0.0
epoch=1
pkgrel=2
url="http://www.xbmc.org/pvr"
arch=('x86_64' 'i686')
license=('GPL2')
pkgdesc="VDR plugin to handle XBMC clients"
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://github.com/opdenkamp/xbmc-pvr-addons.git#commit=$_gitver"
        'vnsiserver-vdr2.1.2compat.diff')
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
backup=('var/lib/vdr/plugins/vnsiserver/allowed_hosts.conf')
install="$pkgname.install"
md5sums=('SKIP'
         'd3748ed104553ad86bc7f052475f1225')

pkgver() {
  cd "${srcdir}/xbmc-pvr-addons"
  git describe --tags | sed 's/-/_/g'
}

prepare() {
  cd "${srcdir}/xbmc-pvr-addons"
  patch -p1 -i "$srcdir/vnsiserver-vdr2.1.2compat.diff"
}

build() {
  cd "${srcdir}/xbmc-pvr-addons/addons/pvr.vdr.vnsi/vdr-plugin-vnsiserver"
  make
}

package() {
  cd "${srcdir}/xbmc-pvr-addons/addons/pvr.vdr.vnsi/vdr-plugin-vnsiserver"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/var/lib/vdr/plugins"
  cp -r vnsiserver "$pkgdir/var/lib/vdr/plugins"

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
