# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-vnsiserver
pkgver=12.0_Frodo
_gitver=96774c4f775b156a46fb58151379dece3e773c96
_vdrapi=2.0.0
epoch=1
pkgrel=1
url="http://www.xbmc.org/pvr"
arch=('x86_64' 'i686')
license=('GPL2')
pkgdesc="VDR plugin to handle XBMC clients"
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=("vdr-api=${_vdrapi}")
source=("git://github.com/opdenkamp/xbmc-pvr-addons.git#commit=$_gitver")
backup=('var/lib/vdr/plugins/vnsiserver/allowed_hosts.conf')
install="$pkgname.install"
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/xbmc-pvr-addons"
  git describe --tags | sed 's/-/_/g'
}

build() {
  cd "${srcdir}/xbmc-pvr-addons/addons/pvr.vdr.vnsi/vdr-plugin-vnsiserver"
  make
}

package() {
  cd "${srcdir}/xbmc-pvr-addons/addons/pvr.vdr.vnsi/vdr-plugin-vnsiserver"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/var/lib/vdr/plugins"
  cp -r vnsiserver "$pkgdir/var/lib/vdr/plugins"
}