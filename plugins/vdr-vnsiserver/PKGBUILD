# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgbase=xbmc-pvr-addons
pkgname=('vdr-vnsiserver' 'xbmc-pvr-addons')
pkgver=12.0
_vdrapi=2.0.0
_codename=Frodo
epoch=1
pkgrel=7
url=""
arch=('x86_64' 'i686')
license=('GPL2')
makedepends=("vdr-api=${_vdrapi}")
source=("https://github.com/opdenkamp/xbmc-pvr-addons/archive/${pkgver}-${_codename}.tar.gz")
md5sums=('4a1020b1a890e26505da7cc9afef1cea')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}-${_codename}/addons/pvr.vdr.vnsi/vdr-plugin-vnsiserver
  make

  cd ${srcdir}/${pkgbase}-${pkgver}-${_codename}
  autoreconf -vfi
  ./configure --prefix=/usr
  make
}

package_vdr-vnsiserver() {
  pkgdesc="VDR plugin to handle XBMC clients"
  depends=('gcc-libs' "vdr-api=${_vdrapi}")
  _plugname=$(echo $pkgname | sed 's/vdr-//g')
  install="$pkgname.install"

  cd ${srcdir}/${pkgbase}-${pkgver}-${_codename}/addons/pvr.vdr.vnsi/vdr-plugin-vnsiserver
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/var/lib/vdr/plugins"
  cp -r vnsiserver "$pkgdir/var/lib/vdr/plugins"
}

package_xbmc-pvr-addons() {
  pkgdesc="XBMC PVR client addons"
  depends=('gcc-libs' 'xbmc')

  cd ${srcdir}/${pkgbase}-${pkgver}-${_codename}
  make DESTDIR="${pkgdir}" install
}
