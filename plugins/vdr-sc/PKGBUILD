# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-sc
pkgver=20120721
_hgver=29b7b5f2
_vdrapi=2.1.3
pkgrel=6
pkgdesc="Software CAM emulation"
url="http://207.44.152.197/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libdvbcsa' 'openssl' "vdr-api=${_vdrapi}")
makedepends=('mercurial')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("$pkgname::hg+http://85.17.209.13:6100#revision=$_hgver"
        'Add_libdvbcsa_support.diff')
md5sums=('SKIP'
         'b00108754f574c0773d30e288b703045')

pkgver() {
  cd "$srcdir/$pkgname"
  hg log -l1 --template "{shortdate(date)}" | sed 's/-//g'
}

prepare() {
  cd "$srcdir/$pkgname"

  patch -p1 -i "$srcdir/Add_libdvbcsa_support.diff"

  #Disable Keyfile support. If you want to watch paytv, pay for it.
  ( cd systems
    ls | grep -v cardclient | xargs rm -r )

  sed -i 's/MAXDVBDEVICES/MAXDEVICES/g' device.c
}

package() {
  cd "$srcdir/$pkgname"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       MAXCAID=64 \
       LIBDVBCSA=1 \
       i18n systems libvdr-$_plugname.so
  install -Dm755 libvdr-$_plugname.so "$pkgdir/usr/lib/vdr/plugins/libvdr-$_plugname.so.$_vdrapi"
}