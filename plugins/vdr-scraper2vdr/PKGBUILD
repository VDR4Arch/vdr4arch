# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-scraper2vdr
pkgver=0.1.0_3_g4f9aa5c
_gitver=4f9aa5ca0187d5d7de763be7020f8c43a6851261
_vdrapi=2.0.6
pkgrel=1
pkgdesc="Acts as client and provides scraped metadata for tvshows and movies from epgd"
url="http://projects.vdr-developer.org/projects/plg-scraper2vdr"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' 'libmariadbclient' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-scraper2vdr.git#commit=$_gitver"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         'b3be8ea56e120b1b0e61d9f53cd9dc26')

pkgver() {
  cd "$srcdir/vdr-plugin-$_plugname"
  git tag -a v0.1.0 -m 'Added Tag' ed8b63540d8fd23268da0e1ddfbad1b21265a6c3 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "$srcdir/vdr-plugin-$_plugname"
  make IMAGELIB=graphicsmagick
}

package() {
  cd "$srcdir/vdr-plugin-$_plugname"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
