# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=1.1.3
_gitver=f593ad572046998285e01ee0f5f767f88e25a94a
_vdrapi=2.0.6
pkgrel=1
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
optdepends=('nopacity-logos: To show channellogos in DisplayChannel view'
            'vdr-scraper2vdr: To display scraped images in various EPG views (requires epgd)'
            'vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/skin-nopacity.git#commit=$_gitver"
        "50-$_plugname.conf"
        'nopacity-always_show_displaychannel.diff')
backup=("var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-light.conf"
        "var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-default.conf"
        "var/lib/vdr/plugins/skinnopacity/themeconfigs/theme-iceblue.conf"
        "etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         'ccdd82d2627a9d2b8d3906cb477be515'
         'c53a82c3b66a44f5b4304a01cdb66632')

pkgver() {
  cd "${srcdir}/skin-nopacity"
  git tag -a 1.1.3 -m 'Added Tag' f593ad572046998285e01ee0f5f767f88e25a94a 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/skin-nopacity"
  patch -p1 -i "$srcdir/nopacity-always_show_displaychannel.diff"
}

build() {
  cd "${srcdir}/skin-nopacity"
  make IMAGELIB=graphicsmagick
}

package() {
  cd "${srcdir}/skin-nopacity"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
