# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=0.9.0_11_gc0d3345
_gitver=c0d3345a116af273f4fb6f94c351566371a0dd40
_vdrapi=2.1.2
pkgrel=3
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('imagemagick=6.8.7.4' 'libpng' "vdr-api=${_vdrapi}")
# libpng because we need imagemagick with libpng support. libpng is only an optdepend of imagemagick
optdepends=('vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/skin-nopacity.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/skin-nopacity"
  git tag -a v0.9.0 -m 'Added Tag' 1aa2f6ace2419406b305f9fe238d9d341f761c73 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/skin-nopacity"
  make
}

package() {
  cd "${srcdir}/skin-nopacity"
  make DESTDIR="$pkgdir" install
}
