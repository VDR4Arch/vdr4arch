# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=0.9.0_41_g1e745b5
_gitver=1e745b560ed4567d938595270f9364e1053400d7
_vdrapi=2.0.0
pkgrel=1
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
optdepends=('vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/skin-nopacity.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/skin-nopacity"
  git tag -a v0.9.0 -m 'Added Tag' 1aa2f6ace2419406b305f9fe238d9d341f761c73 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/skin-nopacity"
  make IMAGELIB=graphicsmagick
}

package() {
  cd "${srcdir}/skin-nopacity"
  make DESTDIR="$pkgdir" install
}
