# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=0.1.4_54_g342afc7
_gitver=342afc759bb81fd1065048893a020c1a38d200aa
_vdrapi=2.0.0
pkgrel=1
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('curl' 'imagemagick' 'libpng' 'libxml2' "vdr-api=${_vdrapi}")
# libpng because we need imagemagick with libpng support. libpng is only an optdepend of imagemagick
optdepends=('vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/skin-nopacity.git#commit=$_gitver")
backup=("var/lib/vdr/plugins/$_plugname/rssfeeds.conf")
options=('!emptydirs')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/skin-nopacity"
  git tag -a v0.1.4 -m 'Added Tag' 7da4695ec136b0ddada153a47ec5d0252f00a214 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/skin-nopacity"
  make
}

package() {
  cd "${srcdir}/skin-nopacity"
  make DESTDIR="$pkgdir" install

  install -Dm644 conf/rssfeeds.conf "$pkgdir/var/lib/vdr/plugins/$_plugname/rssfeeds.conf"
}
