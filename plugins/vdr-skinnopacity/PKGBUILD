# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinnopacity
pkgver=0.1.4
_vdrapi=2.0.0
pkgrel=1
pkgdesc="highly customizable native true color skin for the Video Disc Recorder"
url="http://projects.vdr-developer.org/projects/skin-nopacity"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('curl' 'imagemagick' 'libpng' 'libxml2' "vdr-api=${_vdrapi}")
# libpng because we need imagemagick with libpng support. libpng is only an optdepend of imagemagick
optdepends=('vdr-tvscraper: To display scraped images in various EPG views')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://projects.vdr-developer.org/attachments/download/1445/$pkgname-$pkgver.tgz"
        'skinnopacity-fix_im_gradient_with_hdri.diff')
backup=("var/lib/vdr/plugins/$_plugname/rssfeeds.conf")
options=('!emptydirs')
md5sums=('ed1535a30c01741cad7ff3b280e93c33'
         '40a16c14ee9a86f80f1b1906d7dcae09')

prepare() {
  cd "${srcdir}/$_plugname-$pkgver"
  sed -i '/MAGICKCORE_HDRI_ENABLE/d' Makefile
  sed -i '/MAGICKCORE_QUANTUM_DEPTH/d' Makefile
  sed -i 's/cflags-only-I/cflags/g' Makefile

  patch -p1 -i "$srcdir/skinnopacity-fix_im_gradient_with_hdri.diff"
}

build() {
  cd "${srcdir}/$_plugname-$pkgver"
  make
}

package() {
  cd "${srcdir}/$_plugname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 conf/rssfeeds.conf "$pkgdir/var/lib/vdr/plugins/$_plugname/rssfeeds.conf"
}
