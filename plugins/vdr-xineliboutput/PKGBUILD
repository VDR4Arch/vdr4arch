# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgbase=xineliboutput
pkgname=(vdr-xineliboutput xineliboutput-frontends xineliboutput-xineplug)
pkgver=1.1.0
_gitver=32bde0a1eb887de10e460fe12c32251b6a7f1114
_vdrapi=2.0.6
pkgrel=6
url="http://www.sourceforge.net/projects/xineliboutput"
arch=('x86_64' 'i686' 'armv6h')
license=('GPL2')
makedepends=('dbus-glib' 'git' 'libextractor' 'mesa' "vdr-api=${_vdrapi}" 'xine-lib')
_plugname=$(echo $pkgbase | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/$_plugname.git#commit=$_gitver"
        'xineliboutput-vdr2.1.2compat.diff'
        "50-$_plugname.conf")
md5sums=('SKIP'
         'd313b50c576ab17dfb2a08d50981bec8'
         'a9a3cd09b950d2024b21a1c6e0620506')

pkgver() {
  cd "${srcdir}/${_plugname}"
  git describe --tags | sed "s/$_plugname.//g;s/_/./g;s/-/./g"
}

prepare() {
  cd "${srcdir}/${_plugname}"
  patch -p1 -i "$srcdir/xineliboutput-vdr2.1.2compat.diff"
}

package_vdr-xineliboutput() {
  pkgdesc="X11 and Linux framebuffer front-end for VDR"
  depends=('dbus-glib' 'libextractor' 'mesa' "vdr-api=${_vdrapi}" 'xine-lib' 'xineliboutput-xineplug')
  optdepends=('nvidia: Required for VDPAU decoding'
              'xorg-server: Required for software decoding')
  backup=("etc/vdr/conf.avail/50-$_plugname.conf")
  cd "${srcdir}/${_plugname}"

  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       DESTDIR="$pkgdir" \
       install

  rm -r "$pkgdir/usr/bin"
  rm -r "$pkgdir/usr/lib/xine"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}

package_xineliboutput-frontends() {
  pkgdesc="Xineliboutput frontends (vdr-fbfe and vdr-sxfe)"
  depends=('dbus-glib' 'mesa' 'xine-lib' 'xineliboutput-xineplug')
  optdepends=('nvidia: Required for VDPAU decoding'
              'xorg-server: Required for software decoding')
  cd "${srcdir}/${_plugname}"
  make DESTDIR="$pkgdir" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       install
  rm -r "$pkgdir/usr/lib" "$pkgdir/usr/share"
}

package_xineliboutput-xineplug() {
  pkgdesc="Neccessary xine plugins for xineliboutput-frontends and vdr-xineliboutput"
  depends=('mesa' 'xine-lib')
  cd "${srcdir}/${_plugname}"
  make DESTDIR="$pkgdir" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       install
  rm -r "$pkgdir/usr/bin" "$pkgdir/usr/share"
}
