# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgbase=xineliboutput
pkgname=(vdr-xineliboutput xineliboutput-frontends xineliboutput-xineplug)
pkgver=1.1.0.32.gbf166be
_gitver=bf166be4a21c0a86f6852c4695dcdb06863f4e93
_vdrapi=2.0.6
pkgrel=1
url="http://www.sourceforge.net/projects/xineliboutput"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
makedepends=('dbus-glib' 'git' 'glu' 'libcec' 'libextractor' 'libxrandr' 'mesa' "vdr-api=${_vdrapi}" 'xine-lib')
_plugname=$(echo $pkgbase | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/$_plugname.git#commit=$_gitver"
        'xineliboutput-vdr2.1.6compat.diff'
        "50-$_plugname.conf")
md5sums=('SKIP'
         '14ef92cd38503a2f426669aa43b0aeb4'
         'a9a3cd09b950d2024b21a1c6e0620506')

pkgver() {
  cd "${srcdir}/${_plugname}"
  git describe --tags | sed "s/$_plugname.//g;s/_/./g;s/-/./g"
}

prepare() {
  cd "${srcdir}/${_plugname}"
  patch -p1 -i "$srcdir/xineliboutput-vdr2.1.6compat.diff"
}

build() {
  cd "${srcdir}/${_plugname}"
  make
}

package_vdr-xineliboutput() {
  pkgdesc="X11 and Linux framebuffer front-end for VDR"
  depends=('dbus-glib' 'libextractor' 'libxrandr' "vdr-api=${_vdrapi}" 'xine-lib' 'xineliboutput-xineplug')
  optdepends=('nvidia: Required for VDPAU decoding'
              'xorg-server: Required for software decoding')
  install="$pkgbase.install"
  backup=("etc/vdr/conf.avail/50-$_plugname.conf"
          "var/lib/vdr/plugins/$_plugname/allowed_hosts.conf")
  cd "${srcdir}/${_plugname}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir/usr/bin"
  rm -r "$pkgdir/usr/lib/xine"
  install -Dm644 examples/allowed_hosts.conf "$pkgdir/var/lib/vdr/plugins/$_plugname/allowed_hosts.conf"
  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}

package_xineliboutput-frontends() {
  pkgdesc="Xineliboutput frontends (vdr-fbfe and vdr-sxfe)"
  depends=('dbus-glib' 'libcec' 'libxrandr' 'mesa' 'xine-lib' 'xineliboutput-xineplug')
  optdepends=('nvidia: Required for VDPAU decoding'
              'xorg-server: Required for software decoding')
  cd "${srcdir}/${_plugname}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/lib" "$pkgdir/usr/share"
}

package_xineliboutput-xineplug() {
  pkgdesc="Neccessary xine plugins for xineliboutput-frontends and vdr-xineliboutput"
  depends=('xine-lib')
  cd "${srcdir}/${_plugname}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/bin" "$pkgdir/usr/lib/vdr" "$pkgdir/usr/share"
}
