# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinelchi
pkgver=0.2.8
_vdrapi=2.0.0
pkgrel=18
pkgdesc="adds the 'Elchi' skin to the VDR standard skins"
url="http://firefly.vdr-developer.org/skinelchi/index.html"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://firefly.vdr-developer.org/skinelchi/${pkgname}-${pkgver}.tar.bz2"
        'skinelchi-search_for_logos_in_resourcedir.diff'
        'skinelchi-imagemagick_fix.diff')
md5sums=('b4d3160033fa6541eab55bbf7ad41276'
         'eb414c26fbe46ccfc9cb8ac8cc6e3280'
         '10b5f84b1566810a20d1542502716f2d')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  patch -p1 -i "$srcdir/skinelchi-search_for_logos_in_resourcedir.diff"
  patch -p1 -i "$srcdir/skinelchi-imagemagick_fix.diff"
  sed -i 's/ImageMagick/GraphicsMagick/g' Makefile
  sed -i 's/cflags-only-I/cflags/g' Makefile
}

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  install -Dm644 themes/*.theme "$pkgdir/var/lib/vdr/themes"
}
