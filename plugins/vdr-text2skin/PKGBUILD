# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-text2skin
pkgver=1.3.2_11_g7bd88a0
_gitver=7bd88a078d31f57e8fcfc0b93c45c3e8b13c5190
_vdrapi=2.1.3
pkgrel=11
pkgdesc="Mediaplayer plugin for VDR and X11"
url="http://projects.vdr-developer.org/projects/plg-play"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('vdr4arch-imagemagick-wrapper' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-text2skin.git#commit=$_gitver"
        'text2skin-vdr2.1.2compat.diff')
md5sums=('SKIP'
         '5615fceb3b7597405402d1b817f8bc22')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  patch -p1 -i "$srcdir/text2skin-vdr2.1.2compat.diff"
  sed -i 's/Config/Resource/g' common.c
  sed -i '/^IMAGELIB/d' Makefile
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       IMAGELIB=imagemagick
}
