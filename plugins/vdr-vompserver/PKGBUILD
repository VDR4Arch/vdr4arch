# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-vompserver
pkgver=0.4.0
_vdrapi=2.1.2
pkgrel=4
pkgdesc="Server side of VOMP"
url="http://www.loggytronic.com/vomp.php"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://www.loggytronic.com/dl/$pkgname-$pkgver.tgz"
        'vompserver-vdr2.1.2compat.diff')
md5sums=('3220006cf7faf9536213887b23a9d366'
         '2e98c9d26b56d17263cd58e7840d7aa6')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  patch -p1 -i "$srcdir/vompserver-vdr2.1.2compat.diff"
  sed -i '/VDRDIR =/d' Makefile
}

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  install -Dm 644 vomp.conf.sample "$pkgdir/var/lib/vdr/plugins/vompserver/vomp.conf"
}

