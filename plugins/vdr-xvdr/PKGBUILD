# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-xvdr
pkgver=0.9.9_55_g86b1113
_gitver=86b111301e278929386cd206643fc86c5de3f170
_vdrapi=2.0.6
pkgrel=1
url="https://github.com/pipelka/vdr-plugin-xvdr"
arch=('x86_64' 'i686')
license=('GPL2')
pkgdesc="VDR plugin to handle XBMC clients"
depends=('gcc-libs' "vdr-api=${_vdrapi}")
optdepends=('vdr-wirbelscan: Support channel searches with XBMC')
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git+https://github.com/pipelka/vdr-plugin-xvdr.git#commit=$_gitver"
        "50-$_plugname.conf")
backup=('var/lib/vdr/plugins/xvdr/allowed_hosts.conf'
        'var/lib/vdr/plugins/xvdr/xvdr.conf'
        "etc/vdr/conf.avail/50-$_plugname.conf")
install="$pkgname.install"
md5sums=('SKIP'
         '5927a0c16043a43af28d0a07b88a8dc4')

pkgver() {
  cd "${srcdir}/vdr-plugin-xvdr"
  git describe --tags | sed 's/xvdr-//g;s/-/_/g'
}

build() {
  cd "${srcdir}/vdr-plugin-xvdr"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-xvdr"
  make DESTDIR="${pkgdir}" install install-conf

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
