# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-xvdr
pkgver=0.9.9_33_gae7c45d
_gitver=ae7c45dfaaed071ec8c97c4aa401224a82f2d02c
_vdrapi=2.1.2
pkgrel=2
url="https://github.com/pipelka/vdr-plugin-xvdr"
arch=('x86_64' 'i686')
license=('GPL2')
pkgdesc="VDR plugin to handle XBMC clients"
depends=('gcc-libs' "vdr-api=${_vdrapi}")
optdepends=('vdr-wirbelscan: Support channel searches with XBMC')
makedepends=('git')
source=("git+https://github.com/pipelka/vdr-plugin-xvdr.git#commit=$_gitver")
backup=('var/lib/vdr/plugins/xvdr/allowed_hosts.conf'
        'var/lib/vdr/plugins/xvdr/xvdr.conf')
install="$pkgname.install"
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/vdr-plugin-xvdr"
  git describe --tags | sed 's/xvdr-//g;s/-/_/g'
}

build() {
  cd "${srcdir}/vdr-plugin-xvdr"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-xvdr"
  make DESTDIR="${pkgdir}" install install-conf
}
