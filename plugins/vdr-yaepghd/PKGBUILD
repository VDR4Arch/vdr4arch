# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-yaepghd
pkgver=0.0.4
_gitver=2a08e4e54882a848975d1435e09dece58b7aa2de
_vdrapi=2.0.6
pkgrel=10
pkgdesc="Complete rewrite of the original yaepg plugin"
url="http://sourceforge.net/projects/vdryaepghd"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('graphicsmagick' "vdr-api=${_vdrapi}")
makedepends=('git')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://github.com/lucianm/vdr-plugin-yaepghd.git#commit=$_gitver"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         '6e17635f9d74421f80131621ff347aaa')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  sed -i 's/Magick++/GraphicsMagick++/g' Makefile
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/var/lib/vdr/plugins/"
  cp -r yaepghd "$pkgdir/var/lib/vdr/plugins"

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
