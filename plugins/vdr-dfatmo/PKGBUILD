# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
# Contributor: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgbase=DFAtmo
pkgname=('dfatmo' 'vdr-dfatmo' 'xbmc-addon-dfatmo')
pkgver=0.3.4
_vdrapi=2.0.0
pkgrel=4
epoch=1
url="https://github.com/durchflieger/${pkgbase}"
arch=('x86_64' 'i686')
license=('GPL2')
makedepends=('libusbx' 'python2' "vdr-api=${_vdrapi}" 'zip')
source=("https://github.com/durchflieger/${pkgbase}/archive/v${pkgver}.tar.gz"
        '45-df10ch.rules')
md5sums=('f219c14904b800303565be29a607c52d'
         'c5e0bf17e88febc7e86c7e435f5eea5f')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i "s/python-config/python2-config/" Makefile
  sed -i 's/static const char \*libusb/const char \*libusb/g' df10choutputdriver.c
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  #Compile dfatmo
  CFLAGS+=' -fPIC'
  make DFATMOINSTDIR='/usr' dfatmo

  #Compile vdr plugin
  make DFATMOINSTDIR='/usr' vdrplugin
}

package_dfatmo() {
  pkgdesc="Analyzes the video picture and generates output data for so called 'Atmolight' controllers"
  replaces=('dfatmo-driver')
  conflicts=('dfatmo-driver')
  depends=('libusbx' 'python2')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DFATMOINSTDIR="${pkgdir}/usr" dfatmoinstall
  rm "$pkgdir/usr/lib/dfatmo/dfatmo-xbmc-addon.zip"
}

package_vdr-dfatmo() {
  pkgdesc="VDR plugin to drive a colored back lighting for TVs"
  depends=('dfatmo' 'gcc-libs' "vdr-api=${_vdrapi}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make VDRDESTDIR="${pkgdir}" vdrinstall
  install -Dm644 "${srcdir}/45-df10ch.rules" "${pkgdir}/usr/lib/udev/rules.d/45-df10ch.rules"
}

package_xbmc-addon-dfatmo() {
  pkgdesc="VDR plugin to drive a colored back lighting for TVs"
  depends=('xbmc')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make XBMCDESTDIR="$pkgdir/usr/share/xbmc/addons/script.dfatmo" xbmcinstall
}