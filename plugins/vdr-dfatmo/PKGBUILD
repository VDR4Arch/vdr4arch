# Maintainer: Ole Ernst <olebowle[at]gmx[dot]com>
pkgbase=DFAtmo
pkgname=('dfatmo' 'vdr-dfatmo')
pkgver=0.3.4
_vdrapi=2.0.0
pkgrel=1
epoch=1
url="https://github.com/durchflieger/${pkgbase}"
arch=('x86_64' 'i686')
license=('GPL2')
makedepends=('python2' "vdr-api=${_vdrapi}")
source=("https://github.com/durchflieger/${pkgbase}/archive/v${pkgver}.tar.gz"
        '45-df10ch.rules')
md5sums=('230009ceee0d078e98fcbc6fe7361182'
         'c5e0bf17e88febc7e86c7e435f5eea5f')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  mkdir vdrplug.po
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  #Compile dfatmo
  CFLAGS+=' -fPIC'
  make DFATMOINSTDIR='/usr' dfatmo

  #Compile vdr plugin
  make -f vdr2plug.mk DFATMOINSTDIR='/usr'
}

package_dfatmo() {
  pkgdesc="Analyzes the video picture and generates output data for so called 'Atmolight' controllers"
  replaces=('dfatmo-driver')
  conflicts=('dfatmo-driver')
  depends=('libusbx' 'python2')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -d "${pkgdir}/usr/include" #Workaround
  make DFATMOINSTDIR="${pkgdir}/usr" dfatmoinstall
}

package_vdr-dfatmo() {
  pkgdesc="VDR plugin to drive a colored back lighting for TVs"
  depends=('dfatmo' "vdr-api=${_vdrapi}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -f vdr2plug.mk DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/45-df10ch.rules" "${pkgdir}/usr/lib/udev/rules.d/45-df10ch.rules"
}
