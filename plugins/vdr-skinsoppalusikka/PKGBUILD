# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinsoppalusikka
pkgver=2.0.3
_vdrapi=2.0.6
pkgrel=3
pkgdesc='Standalone skin providing the good old "ElchiAIO" looks'
url="http://www.saunalahti.fi/~rahrenbe/vdr/soppalusikka/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://www.saunalahti.fi/~rahrenbe/vdr/soppalusikka/files/$pkgname-$pkgver.tgz"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('18163ac856a2e1c9193dba6d2828f276'
         'bd9d1d8599cb554fe4a59ceeb08662d8')

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make GITTAG=''
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
