# Maintainer: Alexander Grothe <seahawk1986[at]hotmail[dot]com>
pkgname=vdr-restfulapi
pkgver=0.1.2
pkgrel=10
_vdrapi=2.1.7
pkgdesc="Restful-API Control for VDR"
url="https://github.com/yavdr/vdr-plugin-restfulapi"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('cxxtools' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("https://github.com/yavdr/vdr-plugin-$_plugname/archive/$pkgver.tar.gz"
        'restfulapi-vdr2.1.2compat.diff'
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf"
        'var/lib/vdr/plugins/restfulapi/API.html'
        'var/lib/vdr/plugins/restfulapi/osd.css'
        'var/lib/vdr/plugins/restfulapi/osd.js')
md5sums=('ab76d58b84c6ff0a5c265dace890de20'
         'cfd2e3a393e89598afae97ae0c712925'
         '7b482249828a58f1f6ce2cd4a577f773')

prepare() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"

  patch -p1 -i "$srcdir/restfulapi-vdr2.1.2compat.diff"
  sed -i -e 's/var\/lib\/video\.00/srv\/vdr\/video/' tools.cpp
}

build() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"
  make
}

package() {
  cd "$srcdir/vdr-plugin-$_plugname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -m 644 web/osd.js     "$pkgdir/var/lib/vdr/plugins/restfulapi/"
  install -m 644 web/osd.css    "$pkgdir/var/lib/vdr/plugins/restfulapi/"

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
