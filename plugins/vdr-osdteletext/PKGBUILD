# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-osdteletext
pkgver=0.9.5
_vdrapi=2.3.1
pkgrel=3
pkgdesc="Displays teletext pages directly on VDR's OSD"
url="http://projects.vdr-developer.org/projects/plg-osdteletext"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("http://projects.vdr-developer.org/git/vdr-plugin-$_plugname.git/snapshot/vdr-plugin-$_plugname-$pkgver.tar.bz2"
        "https://github.com/rofafor/vdr-plugin-osdteletext/commit/421ecf1b8a1267ba21693d069a0093ca19af2bd1.diff"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('2d35232f2273d6093061f44d9965ae8a'
         '8f3faa619edc5b667337f6c704e64fc9'
         '4e47fd8f77cea06d30e07486b7fd9d6e')

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}-${pkgver}"
  patch -p1 -i "$srcdir/421ecf1b8a1267ba21693d069a0093ca19af2bd1.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
