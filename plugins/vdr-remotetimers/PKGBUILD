# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-remotetimers
pkgver=1.0.0
_vdrapi=2.1.2
pkgrel=5
pkgdesc="Add/Edit/Remove timers on both, the local (client) VDR and a remote (server) VDR"
url="http://vdr.schmirler.de/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=("vdr-api=${_vdrapi}" 'vdr-svdrpservice')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://vdr.schmirler.de/$_plugname/$pkgname-$pkgver.tgz"
        'remotetimers-vdr2.1.2compat.diff')
md5sums=('db0a06a5901df5f1d6fa3973df3fe456'
         '50b30d5b0251ddf7da1c51d8aa69026a')

prepare() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  patch -p1 -i "$srcdir/remotetimers-vdr2.1.2compat.diff"
}

build() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
