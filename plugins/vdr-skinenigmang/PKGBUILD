# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-skinenigmang
pkgver=0.1.2_16_g0147c0e
epoch=1
_gitver=0147c0ee6222bd10714ef36f42dcee94495bdb92
_vdrapi=2.0.6
pkgrel=1
pkgdesc="skin based on the Enigma text2skin addon"
url="http://andreas.vdr-developer.org/enigmang/index.html"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
optdepends=('enigma-logos: To show channellogos in DisplayChannel view')
makedepends=('git')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-skinenigmang.git#commit=$_gitver"
        'skinenigmang-search_for_logos_in_resourcedir.diff'
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP'
         '6bfd86e7a92d969edd208401b65b20fe'
         '8b2c4d67273e3b52e38496e52b35e345')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  patch -p1 -i "$srcdir/skinenigmang-search_for_logos_in_resourcedir.diff"
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  cp themes/* "$pkgdir/var/lib/vdr/themes"

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
