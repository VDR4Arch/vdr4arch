# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
# Contributor: Ole Ernst <olebowle[at]gmx[dot]com
pkgname=vdr-streamdev
pkgver=0.6.0_40_gf194ca2
epoch=1
_gitver=f194ca2074396beafd0620182ab42b582628c7ca
_vdrapi=2.0.0
pkgrel=1
pkgdesc="implementation of the VTP (Video Transfer Protocol)"
url="http://projects.vdr-developer.org/projects/show/plg-streamdev"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
backup=('var/lib/vdr/plugins/streamdev-server/streamdevhosts.conf')
install="${pkgname}.install"
source=("git://projects.vdr-developer.org/vdr-plugin-streamdev.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git tag -a v0.6.0 -m 'Added Tag' af48d11b1856f69da293ad7808771bd8f31f8ab3 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}


package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="$pkgdir" install

  install -Dm644 streamdev-server/streamdevhosts.conf "$pkgdir/var/lib/vdr/plugins/streamdev-server/streamdevhosts.conf"
}
