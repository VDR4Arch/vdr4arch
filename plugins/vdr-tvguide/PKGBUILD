# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-tvguide
pkgver=1.0.0
epoch=1
_vdrapi=2.0.0
pkgrel=5
pkgdesc="highly customizable 2D EPG viewer plugin."
url="http://projects.vdr-developer.org/projects/plg-tvguide"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('imagemagick=6.8.6.10' "vdr-api=$_vdrapi")
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("http://projects.vdr-developer.org/attachments/download/1402/$pkgname-$pkgver.tgz"
        'tvguide-search_for_logos_in_resourcedir.diff')
md5sums=('72679ad1d7764f886091615e5d5ee688'
         '92de66af2d7cc60d5b6c2628aab5c6a1')

prepare() {
  cd "${srcdir}/$_plugname-$pkgver"
  sed -i '/MAGICKCORE_HDRI_ENABLE/d' Makefile
  sed -i '/MAGICKCORE_QUANTUM_DEPTH/d' Makefile
  sed -i 's/cflags-only-I/cflags/g' Makefile
  patch -p1 -i "$srcdir/tvguide-search_for_logos_in_resourcedir.diff"
}

build() {
  cd "$srcdir/$_plugname-$pkgver"
  make
}

package() {
  cd "$srcdir/$_plugname-$pkgver"
  make DESTDIR="$pkgdir" install
}
