# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-xmltv2vdr
pkgver=0.1.1_71_gb48e0be
_gitver=b48e0bec3c87531c872da028d4fb87c65f3e050e
_vdrapi=2.0.6
pkgrel=1
pkgdesc="Add epg info from epg sources into vdr"
url="http://projects.vdr-developer.org/projects/plg-xmltv2vdr"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('pcre' 'curl' 'enca' 'libxslt' 'libzip' 'sqlite' "vdr-api=${_vdrapi}")
makedepends=('git')
install="$pkgname.install"
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-xmltv2vdr.git#commit=$_gitver"
        "xmltv2vdr_newmakefile.diff::https://github.com/CReimer/vdr-plugin-xmltv2vdr/compare/41b9...master.diff"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
backup=("var/lib/epgsources/epgdata2xmltv")
options=('!emptydirs')
md5sums=('SKIP'
         '57fbda53bd7d6d7164ffbb2bb35a2406'
         'e295ea355768bd373f64eb4b0f760840')

pkgver() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  patch -p1 -i "$srcdir/xmltv2vdr_newmakefile.diff"
}

build() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make
  make -j1 -C dist/epgdata2xmltv
}

package() {
  cd "${srcdir}/vdr-plugin-$_plugname"
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install -C dist/epgdata2xmltv

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
