# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-epg2vdr
pkgver=0.1.10_4_gd264ad8
_gitver=d264ad83ac3b2eb7d0230f9ecf4230cdee1d4f74
_vdrapi=2.0.6
pkgrel=1
pkgdesc="Used to retrieve EPG data into the VDR"
url="http://projects.vdr-developer.org/projects/plg-epg2vdr"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('libmariadbclient' 'libutil-linux' "vdr-api=${_vdrapi}")
makedepends=('git' 'imlib2' 'libxslt')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-epg2vdr.git#commit=$_gitver")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  git tag -a 0.1.10 -m 'Added Tag' 5934544a256b75eaf70c98e58e0dbce186f9875c 2> /dev/null
  git describe --tags | sed 's/-/_/g'
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
