# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr-vdrboblight
pkgver=0.0.5_3_g06464fb
_gitrev=06464fbaa856da447cb794d80e07f5abb96f4b1f
_vdrapi=2.0.6
pkgrel=3
pkgdesc="Boblight with data from softhddevice"
url="http://projects.vdr-developer.org/projects/plg-boblight"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('boblight' 'gcc-libs' "vdr-api=${_vdrapi}")
makedepends=('git')
replaces=('vdr-boblight')
conflicts=('vdr-boblight')
_plugname=$(echo $pkgname | sed 's/vdr-//g')
source=("git://projects.vdr-developer.org/vdr-plugin-boblight.git#commit=$_gitrev")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/vdr-plugin-boblight"
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "${srcdir}/vdr-plugin-boblight"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-boblight"
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
