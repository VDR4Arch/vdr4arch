# Maintainer: Alexander Grothe <seahawk1986[at]hotmail[dot]com>
pkgname=vdr-filebrowser
pkgver=0.2.1
_vdrapi=2.0.0
pkgrel=5
pkgdesc="filebrowser with custom commands for vdr"
url="http://opensource.holgerbrunn.net/vdr/filebrowser/"
arch=('x86_64' 'i686')
license=('GPL3')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=$(echo $pkgname | sed 's/vdr-//g')
replaces=("vdr-plugin-$_plugname")
conflicts=("vdr-plugin-$_plugname")
install="$pkgname.install"
source=("http://opensource.holgerbrunn.net/vdr/filebrowser/$pkgname-$pkgver.tgz")
backup=("var/lib/vdr/plugins/filebrowser/commands.conf"
        "var/lib/vdr/plugins/filebrowser/order.conf"
        "var/lib/vdr/plugins/filebrowser/othercommands.conf"
        "var/lib/vdr/plugins/filebrowser/sources.conf"
        "etc/vdr/conf.avail/50-$_plugname.conf")
md5sums=('a1813fec39604b62c38c3ab44fe4aa5f')

package() {
  cd "${srcdir}/${_plugname}-${pkgver}"

  mkdir -p "$pkgdir/usr/lib/vdr/plugins"
  make CFLAGS="$(pkg-config vdr --variable=cflags)" \
       CXXFLAGS="$(pkg-config vdr --variable=cxxflags)" \
       VDRDIR="/usr/include/vdr" \
       LIBDIR="$pkgdir/$(pkg-config vdr --variable=libdir)" \
       LOCALEDIR="$pkgdir/$(pkg-config vdr --variable=locdir)" \
       all

  mkdir -p "$pkgdir/var/lib/vdr/plugins/filebrowser"
  install -m644 examples/filebrowser/*  "$pkgdir/var/lib/vdr/plugins/filebrowser"

  mkdir -p "$pkgdir/etc/vdr/conf.avail"
  echo "[$_plugname]" > "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}
