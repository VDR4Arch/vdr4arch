# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>

pkgname=pugixml
pkgver=1.5
pkgrel=1
pkgdesc="Light-weight, simple and fast XML parser for C++ with XPath support"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="http://pugixml.org/"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
source=("http://github.com/zeux/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('77a69633cc2fe0858fd177e01e95cb54')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake scripts -DCMAKE_INSTALL_LIBDIR=/usr/lib \
                -DCMAKE_INSTALL_PREFIX=/usr \
                -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '/This library/,//p' readme.txt > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
