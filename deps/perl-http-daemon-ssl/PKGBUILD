# Maintainer: Andreas Baumann <mail@andreasbaumann.cc>
# Contributor: Manuel Reimer <manuel.reimer@gmx.de>

pkgname=perl-http-daemon-ssl
pkgver=1.04
pkgrel=1
pkgdesc="Simple http server class with SSL support"
arch=('any')
url='https://metacpan.org/pod/HTTP::Daemon::SSL'
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-io-socket-ssl' 'perl-io-socket-inet6')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/A/AU/AUFFLICK/HTTP-Daemon-SSL-${pkgver}.tar.gz)
sha256sums=('7eae05422d934c8492dfd3505b774fd6590c99808ce8beb6c57efd261e5dc22e')

build() {
  cd "${srcdir}/HTTP-Daemon-SSL-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/HTTP-Daemon-SSL-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${srcdir}/HTTP-Daemon-SSL-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
