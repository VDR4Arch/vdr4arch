# Maintainer: Christopher Reimer <c[dot]reimer[at]googlemail[dot]com>
# Contributor: Ole Ernst <olebowle[at]gmx[dot]com>
pkgname=graphlcd-base
pkgver=20120106
_gitver=c37e9b48
pkgrel=1
pkgdesc="Project to support graphical LC displays connected to the PC"
url="http://projects.vdr-developer.org/projects/show/graphlcd"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('fontconfig' 'gcc-libs' 'libusb-compat')
optdepends='serdisplib: Add support for more serial displays'
backup=(etc/graphlcd.conf)
source=("http://projects.vdr-developer.org/git/$pkgname.git/snapshot/$pkgname-$_gitver.tar.bz2")
md5sums=('243b6f1d6bb56f9e0c93970166276145')

build() {
  cd "${srcdir}/${pkgname}-${_gitver}"
  sed -i '/CFLAGS/d' Make.config
  sed -i '/CXXFLAGS/d' Make.config
  sed -i '/LDFLAGS/d' Make.config
  sed -i 's/#HAVE_AX206DPF_EXPERIMENTAL/HAVE_AX206DPF_EXPERIMENTAL/' Make.config
  sed -i 's/#HAVE_picoLCD_256x64_EXPERIMENTAL/HAVE_picoLCD_256x64_EXPERIMENTAL/' Make.config
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_gitver}"
  mkdir -p ${pkgdir}/usr/lib/udev/rules.d
  make DESTDIR="${pkgdir}" \
       BINDIR=${pkgdir}/usr/bin \
       LIBDIR=${pkgdir}/usr/lib \
       INCDIR=${pkgdir}/usr/include \
       MANDIR=${pkgdir}/usr/share/man \
       UDEVRULESDIR=${pkgdir}/usr/lib/udev/rules.d \
       install
  install -Dm644 graphlcd.conf "$pkgdir/etc/graphlcd.conf"
}
