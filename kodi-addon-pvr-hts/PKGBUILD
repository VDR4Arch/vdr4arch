# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Manuel Reimer <manuel.reimer@gmx.de>
pkgname=kodi-addon-pvr-hts
pkgver=4.3.8
_gitver=8b66ec3d80527f8803ba3b8db4abb34a18cfde54
pkgrel=3
url="https://github.com/kodi-pvr/pvr.hts"
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
license=('GPL2')
pkgdesc="Kodi's Tvheadend HTSP client addon"
depends=('kodi-devel')
makedepends=('cmake' 'git' 'kodi-devel-dev' 'kodi-platform')
source=("git+https://github.com/kodi-pvr/pvr.hts.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/pvr.hts/pvr.hts"
  grep '  version' addon.xml.in | cut -d'"' -f2
}

build() {
  mkdir -p "${srcdir}/pvr.hts/build"
  cd "${srcdir}/pvr.hts/build"

  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib/kodi \
        -DCMAKE_BUILD_TYPE=Release \
        ..

  make
}

package() {
  cd "${srcdir}/pvr.hts/build"
  make DESTDIR="${pkgdir}" install
}
