# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgbase=vdr-epg-daemon
pkgname=('epgd' 'mariadb-epglv')
pkgver=0.2.0
_gitver=dd4e8eadbe907c533dcf1efb5fa623d26d6f47b7
pkgrel=1
url='http://projects.vdr-developer.org/projects/vdr-epg-daemon'
arch=('x86_64' 'i686')
license=('GPL2')
makedepends=('curl' 'git' 'jansson' 'libarchive' 'libmariadbclient' 'libxslt')
source=("git://projects.vdr-developer.org/$pkgbase.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgbase"
  git tag -a v0.2.0 -m 'Added Tag' dd4e8eadbe907c533dcf1efb5fa623d26d6f47b7 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

build() {
  cd "$srcdir/$pkgbase"
  make PREFIX=/usr
}

package_epgd() {
  pkgdesc='write epg data to a mysql database'
  depends=('curl' 'jansson' 'libarchive' 'libmariadbclient' 'libxslt')
  backup=('etc/epgd/channelmap.conf' 'etc/epgd/epgd.conf')
  cd "$srcdir/$pkgbase"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 contrib/epgd.service "$pkgdir/usr/lib/systemd/system/epgd.service"
  rm "$pkgdir/mysqlepglv.so"
}

package_mariadb-epglv() {
  pkgdesc='epglv plugin for mysql/mariadb'
  depends=('mariadb')
  backup=("$(mysql_config --plugindir | cut -d/ -f2-)/mysqlepglv.so")
  install='mariadb-epglv.install'
  cd "$srcdir/$pkgbase"
  install -Dm755 epglv/mysqlepglv.so "$pkgdir/$(mysql_config --plugindir)/mysqlepglv.so"
}
