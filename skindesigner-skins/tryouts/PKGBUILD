# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
_srcname=tryouts
pkgname=skindesigner-$(echo $_srcname | tr '[:upper:]' '[:lower:]')
pkgver=0.0.2_20_ga4c4586
_gitver=a4c4586f4d967d276f80d38aefabf78e14d7325f
pkgrel=1
pkgdesc="TryoutsHD Skin for SkinDesigner"
arch=('any')
url="https://github.com/BooStars/tryouts"
depends=('vdr-skindesigner')
conflicts=('skindesigner-tryoutshd')
replaces=('skindesigner-tryoutshd')
makedepends=('git')
optdepends=('skindesigner-logos: To show channellogos in DisplayChannel view')
license=('GPL2')
source=("git://github.com/BooStars/tryouts.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/${_srcname}"
  git tag -a 0.0.2 -m 'Added Tag' 49daee1f075e7e37ebf5971effdeb4230ac7b68d 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

package() {
  mkdir -p "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  cp -r $_srcname "$pkgdir/usr/share/vdr/plugins/skindesigner/skins"
  rm -r "$pkgdir/usr/share/vdr/plugins/skindesigner/skins/$_srcname/.git"

  mkdir -p "$pkgdir/var/lib/vdr/themes"
  find $pkgdir -name *.theme -exec mv {} "$pkgdir/var/lib/vdr/themes" \;

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
