# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr
pkgver=2.0.6
pkgrel=4
pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder"
url="http://tvdr.de/"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('libcap' 'libjpeg-turbo' 'perl' 'ttf-font')
optdepends=('lirc-utils: remote control support'
            'ncurses: skincurses plugin'
            'xlogin: To start X11')
replaces=('runvdr-extreme')
conflicts=('runvdr-extreme')
provides=("vdr-api=2.0.6")
options='!emptydirs'
install='vdr.install'
source=("ftp://ftp.tvdr.de/vdr/${pkgname}-${pkgver}.tar.bz2"
        'MainMenuHooks-v1_0_2.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=30330'
        'vdr-2.1.6-conf.d-v6.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=35899'
        'vdr-loglevel-fix.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=33745'
        '10-vdr.conf' '50-dvbsddevice.conf' '50-hello.conf' '50-pictures.conf' '50-rcu.conf'
        '60-create-dvb-device-units.rules'
        'gen-sddropin'
        'shutdown.sh'
        'shutdown-wrapper.c'
        'vdr.service')
backup=("etc/vdr/conf.avail/"50-dvb{s,h}ddevice.conf
        "etc/vdr/conf.avail/"50-{epgtableid0,hello,osddemo,pictures,rcu,skincurses,status,svdrpdemo}.conf
        "etc/vdr/conf.avail/"50-svc{cli,svr}.conf
        'etc/vdr/conf.d/10-vdr.conf'
        'var/lib/vdr/channels.conf'
        'var/lib/vdr/diseqc.conf'
        'var/lib/vdr/keymacros.conf'
        'var/lib/vdr/scr.conf'
        'var/lib/vdr/sources.conf'
        'var/lib/vdr/svdrphosts.conf')
md5sums=('f6916524c302f3209fd0af507ab97387'
         '301c9b9766ed5182b07f1debc79abc21'
         '4a1d2c9b66bd086222da8beffbb48a7e'
         '6877c9efd84261c4a0471e657edce52d'
         '572f5967fe232333e58ad846f53fc677'
         '9cb821ebb6a25e603f9c32bafabde362'
         'fc450f75037b8712673db4969a1dd758'
         'f00583e3f5507b0ff935b4d5919e7df2'
         '1d637e4f97adc66ef84615897116f29a'
         '23d6e1ca0a36cfdbd35d3b1a61f0a105'
         '5fe146509c84500bbf9b1be97b1d5a7d'
         'fb1f52d7decba2df9c5d00c70040984a'
         '7cad811b4ac5ee6c0b5496d006f1e0ee'
         '8ecb8df0b5773bdaf14a9fbed21a1db4')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  touch Make.config
  echo 'CFLAGS      += -O3' >> Make.config
  echo 'CXXFLAGS    += -O3' >> Make.config
  echo 'PREFIX       = /usr' >> Make.config
  echo 'LIBDIR       = /usr/lib/vdr/plugins' >> Make.config
  echo 'VDR_USER     = vdr' >> Make.config

  sed -i 's/NULL, 0, true/NULL, 0, OpenSubMenus/g' "$srcdir/MainMenuHooks-v1_0_2.diff"
  patch -p1 -i "$srcdir/MainMenuHooks-v1_0_2.diff"
  patch -p1 -i "$srcdir/vdr-loglevel-fix.diff"
  sed -i 's/cVideoDirectory::SetName(VideoDirectory);/SetVideoDirectory(VideoDirectory);/g' \
    $srcdir/vdr-2.1.6-conf.d-v6.diff
  patch -p1 -i "$srcdir/vdr-2.1.6-conf.d-v6.diff"
}

build() {
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  for i in dvbsddevice hello pictures rcu; do
    install -Dm644 50-$i.conf "$pkgdir/etc/vdr/conf.avail/50-$i.conf"
  done

  install -Dm644 10-vdr.conf "$pkgdir/etc/vdr/conf.d/10-vdr.conf"
  install -Dm644 60-create-dvb-device-units.rules "$pkgdir/usr/lib/udev/rules.d/60-create-dvb-device-units.rules"
  install -Dm644 vdr.service "$pkgdir/usr/lib/systemd/system/vdr.service"
  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  install -Dm755 gen-sddropin "$pkgdir/usr/lib/vdr/bin/gen-sddropin"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"

  for i in dvbhddevice epgtableid0 osddemo skincurses status svc{cli,svr} svdrpdemo; do
    echo "[$i]" > "$pkgdir/etc/vdr/conf.avail/50-$i.conf"
  done

  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p "$pkgdir/var/lib/vdr"
  make DESTDIR="${pkgdir}" install
}
