# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr
pkgver=2.0.1
pkgrel=1
pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder"
url="http://tvdr.de/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libjpeg-turbo' 'perl' 'ttf-dejavu')
optdepends=('lirc-utils: remote control support'
            'ncurses: skinncurses plugin'
            'runvdr-extreme: startscript for vdr')
provides=("vdr-api=2.0.0")
backup=('var/lib/vdr/channels.conf'
        'var/lib/vdr/diseqc.conf'
        'var/lib/vdr/keymacros.conf'
        'var/lib/vdr/scr.conf'
        'var/lib/vdr/sources.conf'
        'var/lib/vdr/svdrphosts.conf')
options='!emptydirs'
install='vdr.install'
source=("ftp://ftp.tvdr.de/vdr/${pkgname}-${pkgver}.tar.bz2"
        'MainMenuHooks-v1_0_2.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=30330'
	'vdr-1.7.40-ttxtsubs.patch'
        'shutdown.sh'
        'shutdown-wrapper.c')
md5sums=('e1b7a76c57c96300829dccd39eb20e7d'
         '301c9b9766ed5182b07f1debc79abc21'
	 '239727ecce9a79e146aa4ca7866a0e1e'
         'ef8e11062f58a9eb4016dfbf66bb9044'
         '7cad811b4ac5ee6c0b5496d006f1e0ee')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

cat > Make.config <<EOF
CFLAGS      += -O3
CXXFLAGS    += -O3
PREFIX       = /usr
LIBDIR       = /usr/lib/vdr/plugins
VDR_USER     = vdr
EOF

  patch -p1 -i "$srcdir/MainMenuHooks-v1_0_2.diff"
  patch -p1 -i "$srcdir/vdr-1.7.40-ttxtsubs.patch"
}

build() {
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
