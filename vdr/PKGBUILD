# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=vdr
pkgver=2.1.3
pkgrel=2
pkgdesc="'open' digital satellite receiver and timer controlled video disk recorder"
url="http://tvdr.de/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('libjpeg-turbo' 'perl' 'ttf-font')
optdepends=('lirc-utils: remote control support'
            'ncurses: skinncurses plugin'
            'runvdr-extreme: startscript for vdr')
provides=("vdr-api=2.1.3")
backup=('var/lib/vdr/channels.conf'
        'var/lib/vdr/diseqc.conf'
        'var/lib/vdr/keymacros.conf'
        'var/lib/vdr/scr.conf'
        'var/lib/vdr/sources.conf'
        'var/lib/vdr/svdrphosts.conf')
options='!emptydirs'
install='vdr.install'
source=("ftp://ftp.tvdr.de/vdr/Developer/${pkgname}-${pkgver}.tar.bz2"
        'http://skyboo.net/vdr2.1.3-cadescr.diff'
        'MainMenuHooks-v1_0_2.diff::http://www.vdr-portal.de/index.php?page=Attachment&attachmentID=30330'
        'shutdown.sh'
        'shutdown-wrapper.c')
md5sums=('054f80e0045aa6fad118e9285b52f4f2'
         '557508cb1066d0755bb2675376df6576'
         '301c9b9766ed5182b07f1debc79abc21'
         'fb1f52d7decba2df9c5d00c70040984a'
         '7cad811b4ac5ee6c0b5496d006f1e0ee')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

cat > Make.config <<EOF
CFLAGS      += -O3
CXXFLAGS    += -O3
PREFIX       = /usr
LIBDIR       = /usr/lib/vdr/plugins
VDR_USER     = vdr
EOF

  sed -i 's/NULL, 0, true/NULL, 0, OpenSubMenus/g' "$srcdir/MainMenuHooks-v1_0_2.diff"
  patch -p1 -i "$srcdir/MainMenuHooks-v1_0_2.diff"
  patch -p1 -i "$srcdir/vdr2.1.3-cadescr.diff"
}

build() {
  gcc -o shutdown-wrapper shutdown-wrapper.c

  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  install -Dm754 shutdown-wrapper "$pkgdir/usr/lib/vdr/bin/shutdown-wrapper"
  install -Dm755 shutdown.sh "$pkgdir/usr/lib/vdr/bin/shutdown.sh"

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
