# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=runvdr-extreme
pkgver=0.5.0
pkgrel=2
pkgdesc="Runvdr script, just like the runvdr script included in the VDR distribution"
url="http://www.udo-richter.de/vdr/scripts.html"
arch=('any')
license=('GPL2')
depends=('bash' 'vdr')
optdepends=('xorg-xinit: For fireing up a X session')
backup=('etc/runvdr.conf')
source=("http://projects.vdr-developer.org/attachments/download/1536/$pkgname-$pkgver.tar.xz"
        '60-create-dvb-device-units.rules'
        '60-vdr-tty.rules'
        'runvdr.conf'
        'gen-sddropin'
        'vdr.service')
md5sums=('e993264e0ec439118b47689a27111128'
         '23d6e1ca0a36cfdbd35d3b1a61f0a105'
         '32dcab4ce46f8164977ab3abdeccec57'
         'a3bf66614daa536284e517855d2c9dab'
         '5fe146509c84500bbf9b1be97b1d5a7d'
         'a6827951cfd3642fa9df57f128bc58db')

package() {
  install -Dm644 60-create-dvb-device-units.rules "$pkgdir/usr/lib/udev/rules.d/60-create-dvb-device-units.rules"
  install -Dm644 60-vdr-tty.rules "$pkgdir/usr/lib/udev/rules.d/60-vdr-tty.rules"
  install -Dm644 vdr.service "$pkgdir/usr/lib/systemd/system/vdr.service"
  install -Dm644 runvdr.conf "$pkgdir/etc/runvdr.conf"
  install -Dm755 gen-sddropin "$pkgdir/usr/lib/vdr/bin/gen-sddropin"

  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 runvdr "$pkgdir/usr/bin/runvdr"

  mkdir -p "$pkgdir/usr/share/man/man5"
  /usr/bin/core_perl/pod2man -s 5 -c runvdr -r runvdr runvdr.conf.pod "$pkgdir/usr/share/man/man5/runvdr.conf.5.gz"
}
