# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=v4l-dvb-saa716x
pkgver=20130224
_hgver=cfa4b4faab67
_extramodules=extramodules-3.9-ARCH
url='http://powarman.dyndns.org/hgwebdir.cgi/v4l-dvb-saa716x'
pkgrel=3
pkgdesc="All interesting modules of powarman's v4l-dvb-saa716x repository"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('linux>=3.9' 'linux<3.10')
makedepends=('linux-headers>=3.9' 'linux-headers<3.10' 'mercurial')
install="${pkgname}.install"
replaces=('saa716x')
conflicts=('saa716x')
source=("hg+http://linuxtv.org/hg/~endriss/mirror-saa716x#revision=$_hgver"
        'compat.diff')
md5sums=('SKIP'
         '463dbc6891d8648d8ae213ac92203bdd')

pkgver() {
  cd "$srcdir/mirror-saa716x"
  hg log -l1 --template "{shortdate(date)}" | sed 's/-//g'
}

prepare() {
  cd "$srcdir/mirror-saa716x"
  patch -p1 -i "$srcdir/compat.diff"
}

build() {
  _kernver="$(cat /lib/modules/$_extramodules/version)"

  cd "$srcdir/mirror-saa716x/linux/drivers/media/common/saa716x"
  echo 'ccflags-y += -Idrivers/media/tuners/' >> Makefile
  make -C /lib/modules/$_kernver/build M=$(pwd) CONFIG_SAA716X_CORE=m CONFIG_DVB_SAA716X_BUDGET=m \
    CONFIG_DVB_SAA716X_HYBRID=m CONFIG_DVB_SAA716X_FF=m modules
}


package() {
  cd "$srcdir"

  mkdir -p "$pkgdir/usr/lib/modules/$_extramodules"
  find -name *.ko -exec cp {} "$pkgdir/usr/lib/modules/$_extramodules" \;
  gzip -9 "$pkgdir/usr/lib/modules/$_extramodules/"*
}
