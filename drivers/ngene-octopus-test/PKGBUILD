# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=ngene-octopus-test
pkgver=20130307
_hgver=406ffecccbec
_kernver=3.11.2
pkgrel=9
url='http://linuxtv.org/hg/~endriss/ngene-octopus-test'
pkgdesc="All interesting modules of endriss' driver repository"
arch=('x86_64' 'i686')
license=('GPL2')
depends=("linux=$_kernver")
makedepends=("linux-headers=$_kernver" 'mercurial')
install="${pkgname}.install"
replaces=('ddbridge')
conflicts=('ddbridge')
source=("hg+http://linuxtv.org/hg/~endriss/$pkgname#revision=$_hgver"
        "cxd2099.h::https://git.kernel.org/cgit/linux/kernel/git/stable/linux-stable.git/plain/drivers/staging/media/cxd2099/cxd2099.h?id=v${_kernver}")
md5sums=('SKIP'
         'b7903ec3ca22610123249dbc11c50026')


pkgver() {
  cd "$srcdir/$pkgname"
  hg log -l1 --template "{shortdate(date)}" | sed 's/-//g'
}

build() {
  cd "$srcdir/$pkgname/linux/drivers/media/dvb"

  pushd ddbridge
  sed -i 's/dvb\///g' Makefile
  sed -i 's/frontends/dvb-frontends/g' Makefile
  sed -i 's/common\///g' Makefile
  ln -sf "$srcdir/cxd2099.h" ../frontends/{stv0367dd.h,tda18212dd.h} .
  make -C /usr/src/linux-$_kernver-*-ARCH M=$(pwd) CONFIG_DVB_DDBRIDGE=m modules
  popd

  pushd ngene
  echo 'ccflags-y += -Iinclude/linux/' >> Makefile
  sed -i 's/dvb\///g' Makefile
  sed -i 's/frontends/dvb-frontends/g' Makefile
  sed -i 's/common\///g' Makefile
  ln -sf "$srcdir/cxd2099.h" ../frontends/{stv0367dd.h,tda18212dd.h} .
  make -C /usr/src/linux-$_kernver-*-ARCH M=$(pwd) CONFIG_DVB_NGENE=m modules
  popd

  pushd frontends
  echo 'obj-m += stv0367dd.o' > Makefile
  echo 'obj-m += tda18212dd.o' >> Makefile
  echo 'ccflags-y += -Iinclude/linux/' >> Makefile
  echo 'ccflags-y += -Idrivers/media/dvb-core/' >> Makefile
  make -C /usr/src/linux-$_kernver-*-ARCH M=$(pwd) modules
}

package() {
  mkdir -p "$pkgdir/usr/lib/modules/extramodules-${_kernver%.*}-ARCH"
  find "$srcdir" -name *.ko -exec cp {} "$pkgdir/usr/lib/modules/extramodules-${_kernver%.*}-ARCH" \;
  gzip -9 "$pkgdir/usr/lib/modules/extramodules-${_kernver%.*}-ARCH/"*
}
