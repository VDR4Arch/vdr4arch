# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
# Contributor: Ole Ernst <olebowle[at]gmx[dot]com>
pkgbase=drivers
pkgname=('digitaldevices-dvb-drivers' 'dvbsky-dvb-drivers' 'technotrend-dvb-drivers')
pkgver=3.17.2
pkgrel=1
_extramodules=extramodules-3.17-ARCH
_saa716xrev=432762831b67a456254fd291f4921e8ed75ae43b
_media_buildrev=04cd0952eb04
url='https://github.com/CReimer/vdr4arch'
pkgdesc="Additional or updated dvb driver modules"
arch=('x86_64' 'i686')
license=('GPL2')
depends=("linux=$pkgver")
makedepends=('mercurial' "linux-headers=$pkgver")
install="${pkgbase}.install"
source=("https://www.kernel.org/pub/linux/kernel/v3.x/linux-$pkgver.tar.xz"
        'http://download.digital-devices.de/download/linux/dddvb-0.9.15a.tar.bz2'
        'kernel-3.17-dvbsky.patch'
        'http://dvbsky.net/download/linux/media_build-bst-14-141014.tar.gz'
        "http://linuxtv.org/hg/~endriss/media_build_experimental/raw-file/$_media_buildrev/experimental/saa716x."{include-osd,pci-kconfig,pci-makefile}.diff
        "http://linuxtv.org/hg/~endriss/media_build_experimental/raw-file/$_media_buildrev/experimental/patch.d/320_stv090x_ucblocks_ber.diff"
        "http://linuxtv.org/hg/~endriss/media_build_experimental/raw-file/$_media_buildrev/experimental/patch.d/020_ddbridge-drxk.diff"
        '010_dd-frontends-kconfig.diff'
        '010_dd-frontends-kconfig2.diff'
        '010_dd-frontends-makefile.diff'
        'backport-dddvb.diff'
        "hg+https://bitbucket.org/powARman/v4l-dvb-saa716x#revision=$_saa716xrev")
md5sums=('87444e6f0906e2ec783cf0ed6aab1a56'
         'e7f313ce5b548aeb55d168ad8b34b91a'
         '0e06a33e1b48c6daaef920b94338087e'
         '728907f942177c7fafcfac5d43457f33'
         '53d2ede9c39f76f62b4a600451e97481'
         'fd63ea01a4f7e62682180fc925a4ac88'
         'd7e5fa4559918594ef2f84908f04a27d'
         '109da4ad9b692b866cbf7eba6b483ecb'
         '693ae5fe2672d44e153b99aca7c72ab0'
         '485eea60abca891ce6cb291bb7f971de'
         'dcb9acaa0496cd430855c0defd2dcf5a'
         '7c18c0eed49413014dedaaffa4dd8a0d'
         'a185170b5470a1151ecfaa838a157dc1'
         'SKIP')

#dvbsky server doesn't like curl's user agent
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

prepare() {
  if ! grep -q "EXTRAMODULES=$_extramodules" "$startdir/$pkgbase.install"; then
    sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${pkgbase}.install"
  fi

  #dd
  cd "$srcdir/dddvb-0.9.15a"
  patch -p1 -i "$srcdir/backport-dddvb.diff"
  cp $srcdir/linux-$pkgver/drivers/media/dvb-frontends/drxk.h $srcdir/linux-$pkgver/drivers/media/pci/cx23885/drxk.h
  cp $srcdir/linux-$pkgver/drivers/media/dvb-frontends/drxk.h $srcdir/linux-$pkgver/drivers/media/usb/dvb-usb-v2/drxk.h
  cp frontends/{stv0367dd*,tda18212dd*,cxd2843*,lnbh25*,stv6111*,stv0910*} $srcdir/linux-$pkgver/drivers/media/dvb-frontends/
#   cp frontends/cxd2099* $srcdir/linux-$pkgver/drivers/staging/media/cxd2099/
  install -Dm644 include/linux/dvb/mod.h $srcdir/linux-$pkgver/include/linux/dvb/mod.h

  rm "$srcdir/linux-$pkgver/drivers/media/pci/ddbridge/"{*.c,*.h,Makefile}
  cp -r ddbridge "$srcdir/linux-$pkgver/drivers/media/pci/"

  cd "$srcdir/linux-$pkgver"
  patch -p1 -i "$srcdir/010_dd-frontends-kconfig.diff"
  patch -p1 -i "$srcdir/010_dd-frontends-kconfig2.diff"
  patch -p1 -i "$srcdir/010_dd-frontends-makefile.diff"
  patch -p1 -i "$srcdir/020_ddbridge-drxk.diff"
  sed -i 's/0666/0664/' drivers/media/pci/ddbridge/ddbridge-core.c

  #dvbsky
  cd "$srcdir/linux-$pkgver"
  ln -sf "$srcdir"/media_build-bst-14/v4l/sit2_op.o.x${CARCH: -2} drivers/media/dvb-frontends/sit2_op.o
  ln -sf "$srcdir"/media_build-bst-14/v4l/sit2_mod.dvb drivers/media/dvb-frontends/sit2_mod.c
  patch -Np1 -i "$srcdir/kernel-3.17-dvbsky.patch"

  #technotrend
  echo 'EXTRA_CFLAGS += -Idrivers/media/tuners' >> "$srcdir/v4l-dvb-saa716x/linux/drivers/media/common/saa716x/Makefile"
  cd "$srcdir"/linux-$pkgver
  patch -p1 -i "$srcdir/saa716x.include-osd.diff"
  patch -p1 -i "$srcdir/saa716x.pci-kconfig.diff"
  patch -p1 -i "$srcdir/saa716x.pci-makefile.diff"
  patch -p1 -i "$srcdir/320_stv090x_ucblocks_ber.diff"
  cp -r "$srcdir/v4l-dvb-saa716x/linux/drivers/media/common/saa716x" drivers/media/pci
}

build() {
  #Prepare full kernel source. linux-headers package is incomplete.
  cd "$srcdir/linux-$pkgver"
  cp /usr/lib/modules/$pkgver*-ARCH/build/.config .
  cp /usr/lib/modules/$pkgver*-ARCH/build/Module.symvers .
  echo CONFIG_SAA716X_SUPPORT=y >> .config
  yes 'm' | make oldconfig
  make prepare
  make scripts

#   staging/media/cxd2099 \
  for _folder in media/dvb-frontends \
                 media/pci/cx88 \
                 media/pci/cx23885 \
                 media/usb/dvb-usb-v2 \
                 media/pci/ddbridge \
                 media/pci/saa716x \
                 media/pci/smipcie \
                 media/rc/keymaps; do
    cd "$srcdir/linux-$pkgver/drivers/$_folder"
    make -C "$srcdir"/linux-$pkgver M=$(pwd) modules
  done
}

package_digitaldevices-dvb-drivers() {
  pkgdesc+="(Digital Devices/Linux4Media)"
  replaces=('dddvb' 'ngene-octopus-test')
  conflicts=('dddvb' 'ngene-octopus-test')
  mkdir -p "$pkgdir"/usr/lib/modules/$_extramodules
  cd "$pkgdir"/usr/lib/modules/$_extramodules
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/ddbridge/ddbridge.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/stv0367dd.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/tda18212dd.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/cxd2843.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/lnbh25.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/stv6111.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/stv0910.ko .
#   mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/drxk.ko .
#   mv "$srcdir"/linux-$pkgver/drivers/staging/media/cxd2099/cxd2099.ko .
  gzip -9 *
}

package_dvbsky-dvb-drivers() {
  pkgdesc+="(DVBSky)"
  replaces=('media-build-dvbsky')
  conflicts=('media-build-dvbsky')
  license=('GPL2') #sit2.o is obviously closed source. The license seems to be GPL2 anyway.
  mkdir -p "$pkgdir"/usr/lib/modules/$_extramodules
  cd "$pkgdir"/usr/lib/modules/$_extramodules
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/m88dc2800.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/dvbsky_m88{ds3103,rs6000}.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/sit2fe.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/cx23885/cx23885.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/cx88/cx88{-dvb,00,xx}.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/smipcie/smipcie.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/rc/keymaps/rc-dvbsky.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/usb/dvb-usb-v2/dvb-usb-dvbsky.ko .
  gzip -9 *
}

package_technotrend-dvb-drivers() {
  pkgdesc+="(TechnoTrend)"
  replaces=('v4l-dvb-saa716x')
  conflicts=('v4l-dvb-saa716x')
  mkdir -p "$pkgdir"/usr/lib/modules/$_extramodules
  cd "$pkgdir"/usr/lib/modules/$_extramodules
  mv "$srcdir"/linux-$pkgver/drivers/media/dvb-frontends/stv090x.ko .
  mv "$srcdir"/linux-$pkgver/drivers/media/pci/saa716x/*.ko .
  gzip -9 *
}
