# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=media_build-bst
pkgver=130709
_extramodules=extramodules-3.10-ARCH
url='http://www.dvbsky.net/Support.html'
pkgrel=3
pkgdesc="All interesting modules of dvbsky's driver package"
arch=('x86_64')
license=('GPL2')
depends=('linux>=3.10' 'linux<3.11')
makedepends=('linux-headers>=3.10' 'linux-headers<3.11')
install="${pkgname}.install"
source=("http://www.dvbsky.net/download/linux/$pkgname-$pkgver.tar.gz"
        'compat.diff')
md5sums=('212b031f5b8f66f95716aca3e4ed23ef'
         '9a4126e3219473a1408b25f02f092316')

#sever doesn't like curl's user agent
DLAGENTS="$( IFS=$'\n'; echo "${DLAGENTS[*]}" | grep '^http::' ) --user-agent 'Mozilla/4.0'"

prepare() {
  cd "$srcdir/$pkgname"
  patch -p1 -i "$srcdir/compat.diff"
}

build() {
  _kernver="$(cat /lib/modules/$_extramodules/version)"

  cd $srcdir/$pkgname/linux/drivers/media/pci/cx23885
  sed -i 's/<misc\/altera.h>/"altera.h"/g' cx23885-cards.c
  sed -i '1 i\#define RC_MAP_DVBSKY "rc-dvbsky"' cx23885-input.c
  ln -sf $srcdir/$pkgname/linux/include/misc/altera.h .
  ln -sf $srcdir/$pkgname/linux/drivers/media/i2c/btcx-risc.h .
  ln -sf $srcdir/$pkgname/linux/drivers/media/dvb-frontends/{m88dc2800.h,m88ds3103.h,sit2.h} .
  make -C /lib/modules/$_kernver/build M=$(pwd) CONFIG_VIDEO_CX23885=m modules

  cd $srcdir/$pkgname/linux/drivers/media/dvb-frontends
  ln -sf $srcdir/$pkgname/v4l/sit2.o.x64 sit2.o
  echo 'sit2fe-objs := sit2.o kmem_func.o' > Makefile
  echo 'obj-m += m88ds3103.o' >> Makefile
  echo 'obj-m += m88dc2800.o' >> Makefile
  echo 'obj-m += sit2fe.o' >> Makefile
  echo 'ccflags-y += -DCONFIG_DVB_M88DS3103=m' >> Makefile
  echo 'ccflags-y += -DCONFIG_DVB_M88DC2800=m' >> Makefile
  echo 'ccflags-y += -DCONFIG_DVB_SIT2=m' >> Makefile
  echo 'ccflags-y += -Idrivers/media/dvb-core/' >> Makefile
  make -C /lib/modules/$_kernver/build M=$(pwd) modules

  cd $srcdir/$pkgname/linux/drivers/media/usb/dvb-usb-v2
  ln -sf $srcdir/$pkgname/linux/drivers/media/dvb-frontends/{m88ds3103.h,sit2.h} .
  sed -i '1 i\#define RC_MAP_DVBSKY "rc-dvbsky"' dvbsky.c
  echo 'dvb-usb-dvbsky-objs := dvbsky.o' > Makefile
  echo 'obj-m += dvb-usb-dvbsky.o' >> Makefile
  echo 'ccflags-y += -DCONFIG_USB_DVBSKY=m' >> Makefile
  echo 'ccflags-y += -Idrivers/media/dvb-core/' >> Makefile
  make -C /lib/modules/$_kernver/build M=$(pwd) modules

  cd $srcdir/$pkgname/linux/drivers/media/i2c
  echo 'obj-m  += btcx-risc.o' > Makefile
  echo 'obj-m += cx2341x.o' >> Makefile
  echo 'obj-m += tveeprom.o' >> Makefile
  make -C /lib/modules/$_kernver/build M=$(pwd) modules

  cd $srcdir/$pkgname/linux/drivers/media/rc/keymaps
  sed -i '1 i\#define RC_MAP_DVBSKY "rc-dvbsky"' rc-dvbsky.c
  echo 'obj-m  += rc-dvbsky.o' > Makefile
  make -C /lib/modules/$_kernver/build M=$(pwd) modules

  cd $srcdir/$pkgname/linux/drivers/media/pci/cx88
  sed -i '1 i\#define RC_MAP_DVBSKY "rc-dvbsky"' cx88-input.c
  ln -sf $srcdir/$pkgname/linux/drivers/media/i2c/btcx-risc.h .
  ln -sf $srcdir/$pkgname/linux/drivers/media/dvb-frontends/m88ds3103.h .
  make -C /lib/modules/$_kernver/build M=$(pwd) modules
}

package() {
  cd "$srcdir/$pkgname"

  mkdir -p "$pkgdir/usr/lib/modules/$_extramodules"
  find -name *.ko -exec cp {} "$pkgdir/usr/lib/modules/$_extramodules" \;
  gzip -9 "$pkgdir/usr/lib/modules/$_extramodules/"*
}
