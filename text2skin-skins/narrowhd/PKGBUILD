# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
_srcname=NarrowHD
pkgname=text2skin-$(echo $_srcname | tr '[:upper:]' '[:lower:]')
pkgver=0.2
pkgrel=1
pkgdesc="Full HD skin inital based on the famous PearlHD skin"
arch=('any')
url='https://vdr-projects.e-tobi.net/projects/skin-narrowhd'
depends=('vdr-text2skin')
license=('GPL2')
source=("${_srcname}_v$pkgver.zip::https://www.vdr-portal.de/index.php?attachment/35935")
sha256sums=('badbf64aecdde7883dce25ae8641de0707bdad89516fbaa6d251aef4fa35dd42')

# SSL certificate on www.vdr-portal.de is regularly broken so don't check it
DLAGENTS=('https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 --insecure -o %o %u')

prepare() {
  cd "$srcdir/${_srcname}_v$pkgver"
  sed -i 's|\.\.\/\.\.\/\.\.\/channellogos|\/usr\/share\/vdr\/plugins\/text2skin\/narrowhdlogos|g' $_srcname.skin
}

package() {
  cd "$srcdir/${_srcname}_v$pkgver"
  install -Dm644 $_srcname.colors "$pkgdir/usr/share/vdr/plugins/text2skin/$_srcname/$_srcname.colors"
  install -Dm644 $_srcname.skin "$pkgdir/usr/share/vdr/plugins/text2skin/$_srcname/$_srcname.skin"
  install -Dm644 themes/$_srcname-default.theme "$pkgdir/var/lib/vdr/themes/$_srcname-default.theme"

  chown -R 666:666 "$pkgdir/var/lib/vdr"
}
