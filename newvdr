#!/usr/bin/perl
# newvdr - Helper script to spread the VDR version number to plugin PKGBUILD's
# Copyright (C) 2019 Manuel Reimer <manuel.reimer@gmx.de>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

use strict;
use warnings;
use Cwd;

my $VDRPKGBUILD = 'vdr/PKGBUILD';
my $PLUGINSPATH = 'plugins';

{
  # Check privileges. Never run as root!
  die("This script can't be executed as root!\n") if ($< == 0);

  # Read VDR's API version
  my $vdrversion;
  open(my $fh, '<', $VDRPKGBUILD) or die("Failed to read VDR PKGBUILD\n");
  while (my $line = <$fh>) {
    ($vdrversion) = $line =~ /^_vdrapi=(\d+\.\d+\.\d+)/ and last;
  }
  close($fh);
  die ("Can't read vdr-api from '$VDRPKGBUILD'\n") unless ($vdrversion);

  # Loop over the plugins
  my $counter = 0;
  opendir(my $dhplugins, $PLUGINSPATH) or die("Can't open path $PLUGINSPATH\n");
  while (my $subdir = readdir($dhplugins)) {
    next if ($subdir =~ /^\./);
    my $plugindir = "$PLUGINSPATH/$subdir";
    next unless (-d $plugindir);

    # Read full plugin PKGBUILD content
    open($fh, '<', "$plugindir/PKGBUILD")
      or die("Can't read PKGBUILD in $plugindir\n");
    my $pkgbuildcontent = join('', <$fh>);
    close($fh);

    # Parse out variables
    my ($vdrapi) = $pkgbuildcontent =~ /^_vdrapi=(.+)/m
      or die("Can't read _vdrapi for $plugindir\n");
    my ($pkgrel) = $pkgbuildcontent =~ /^pkgrel=(\d+)/m
      or die("Can't read pkgrel for $plugindir\n");

    # Anything to do for us?
    next if ($vdrapi eq $vdrversion);

    # Count up pkgrel
    $pkgrel++;

    # Edit PKGBUILD content
    $pkgbuildcontent =~ s/^_vdrapi=.+$/_vdrapi=$vdrversion/m;
    $pkgbuildcontent =~ s/^pkgrel=.+$/pkgrel=$pkgrel/m;

    # Write out changes
    open($fh, '>', "$plugindir/PKGBUILD")
      or die("Can't write PKGBUILD in $plugindir\n");
    print $fh $pkgbuildcontent;
    close($fh);

    # Update .SRCINFO as repo-make won't do it for "disabled" plugins
    my $oldcwd = getcwd();
    chdir($plugindir);
    system('makepkg --nosign --printsrcinfo > .SRCINFO');
    die("Failed to get SRCINFO with makepkg") if ($?);
    chdir($oldcwd);

    # Count up
    $counter++;
  }

  if ($counter > 1) {
    print "Successfully edited $counter PKGBUILD's\n";
    print "Don't forget to review the automatic changes via 'git diff'!\n";
  }
  else {
    print "Nothing to do!\n";
  }
}
