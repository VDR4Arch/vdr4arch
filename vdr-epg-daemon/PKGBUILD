# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgbase=vdr-epg-daemon
pkgname=('epgd' 'mariadb-epglv')
pkgver=0.2.1
_gitver=e1d19b88655144471daae1fade64b08b55606e1e
pkgrel=2
url='http://projects.vdr-developer.org/projects/vdr-epg-daemon'
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
makedepends=('curl' 'git' 'imlib2' 'jansson' 'libarchive' 'libjpeg' 'libmariadbclient' 'libxslt')
source=("git://projects.vdr-developer.org/$pkgbase.git#commit=$_gitver")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgbase"
  git tag -a v0.2.1 -m 'Added Tag' e1d19b88655144471daae1fade64b08b55606e1e 2> /dev/null
  git describe --tags | sed 's/-/_/g;s/v//'
}

prepare() {
  cd "$srcdir/$pkgbase"/epglv
  sed -i '/CPP_FLAGS_32/d' Makefile
  sed -i '/CPP_FLAGS_64/d' Makefile
}

build() {
  cd "$srcdir/$pkgbase"
  make PREFIX=/usr
}

package_epgd() {
  pkgdesc='write epg data to a mysql database'
  depends=('curl' 'jansson' 'libarchive' 'libmariadbclient' 'libxslt')
  cd "$srcdir/$pkgbase"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 contrib/epgd.service "$pkgdir/usr/lib/systemd/system/epgd.service"
  rm "$pkgdir/mysqlepglv.so"
  cd $pkgdir
  backup=($(find etc -type f))
}

package_mariadb-epglv() {
  pkgdesc='epglv plugin for mysql/mariadb'
  depends=('libmariadbclient')
  backup=("$(mysql_config --plugindir | cut -d/ -f2-)/mysqlepglv.so")
  install='mariadb-epglv.install'
  cd "$srcdir/$pkgbase"
  install -Dm755 epglv/mysqlepglv.so "$pkgdir/$(mysql_config --plugindir)/mysqlepglv.so"
}
